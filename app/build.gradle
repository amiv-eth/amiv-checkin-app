apply plugin: 'com.android.application'
apply plugin: 'com.github.triplet.play'

// Load keystore
def keystorePropertiesFile = file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'

    playAccountConfigs {
        defaultAccountConfig {
            jsonFile = file('google-play-key.json')
        }
    }

    signingConfigs {
        release {
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }
    }

    defaultConfig {
        applicationId "ch.amiv.checkin"
        minSdkVersion 19
        targetSdkVersion 27
        versionCode 13
        versionName "1.2"
        multiDexEnabled true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        setProperty("archivesBaseName", "amiv-check-in-$versionName")
        playAccountConfig = playAccountConfigs.defaultAccountConfig
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    testImplementation 'junit:junit:4.12'
    implementation 'com.android.support:appcompat-v7:27.1.0'
    implementation 'com.android.support:design:27.1.0'
    implementation 'com.mcxiaoke.volley:library:1.0.19'
    implementation 'com.android.support:multidex:1.0.3'

    //implementation 'com.google.android.gms:play-services:11.8.0' //Bundled version of google play libaries, only use required libaries, see https://stackoverflow.com/questions/43407318/how-to-suppress-the-avoid-using-bundled-version-of-google-play-services-sdk-wa/43407398
    implementation 'com.google.android.gms:play-services-vision:11.8.0'
    //implementation 'pl.allegro.android:fogger:0.9.0'
}

play {
    track = 'production'
    uploadImages = true
    errorOnSizeLimit = true
}
